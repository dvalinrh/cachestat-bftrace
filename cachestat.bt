#!/usr/local/bin/bpftrace
//
// cachestate:  Monitors the hit and miss to the page cache and reports
//              the values at the designated interval (default is 5).
//
// Created by David Valin.  02/22/2019

BEGIN
{
	if ($1 != 0) {
		@time_pass = $1;
	} else {
		@time_pass = 1;
	};
	
	@how_many = $2;
	@running = 1;
	@time_count = 0;
	@grand_total_hits = 0;
	@grand_total_miss = 0;
}

kprobe:pagecache_get_page
{
	@pgp[tid] = 1;
}

kretprobe:pagecache_get_page
{
	@pgp[tid] = 0;
}

kretprobe:find_get_entry
	/ @pgp[tid] == 1 && retval != 0 /
{
	@hits = @hits+1;
}

kretprobe:find_get_entry
	/ @pgp[tid] == 1 &&retval == 0 /
{
	@miss = @miss+1;
}

interval:s:1
{
	if (@running == 1 && @time_count != @time_pass) {
		@time_count = @time_count+1;
	} else {
		if (@how_many != 0) {
			@how_many = @how_many -1;
			if (@how_many == 0) {
				@running = 0;
			}
		};
		@time_count = 0;
		if (@passes == 0) {
			printf("%12s%12s%12s\n", "Hits", "Misses","%hits");
			@passes = 20;
		};
        	@passes = @passes - 1;
		$tmp_hits = @hits;
		$tmp_miss = @miss;
		$pct_hits = ($tmp_hits*100)/($tmp_hits + $tmp_miss);
		printf("%12d%12d%12d%% ", $tmp_hits, $tmp_miss, $pct_hits);
		time("%H:%M:%S\n");

		@grand_total_miss = @grand_total_miss+@miss;	
		@grand_total_hits = @grand_total_hits+@hits;	
		clear(@miss);
		clear(@hits);
	};
	if (@running == 0) {
// Exit does not go into end.  Repeat what is in END.
		printf("\nSummary\n");
		printf("total hits: %12d\n", @grand_total_hits);
		printf("total miss: %12d\n", @grand_total_miss);
		printf("Hit percentage %12d\n", (@grand_total_hits*100)/(@grand_total_hits + @grand_total_miss));
		delete(@running);
		delete(@how_many);
		delete(@passes);
		delete(@grand_total_miss);
		delete(@grand_total_hits);
		delete(@miss);
		clear(@hits);
		delete(@hits);
		clear(@pgp);
		delete(@pgp);
		delete(@pgp);
		delete(@time_count);
		delete(@time_pass);
		exit();
	};
}

END
{
	printf("\nSummatry\n");
	printf("total hits: %12d\n", @grand_total_hits);
	printf("total miss: %12d\n", @grand_total_miss);
	printf("Hit percentage %12d\n", (@grand_total_hits*100)/(@grand_total_hits + @grand_total_miss));
	delete(@running);
	delete(@how_many);
	delete(@passes);
	delete(@grand_total_miss);
	delete(@grand_total_hits);
	delete(@miss);
	delete(@hits);
	clear(@pgp);
	delete(@pgp);
	delete(@pgp);
	delete(@time_count);
	delete(@time_pass);
}
